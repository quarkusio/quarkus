plugins {
    id 'java'
    id 'io.quarkus'
}

repositories {
    mavenLocal {
        content {
            includeGroupByRegex 'io.quarkus.*'
            includeGroup 'org.hibernate.orm'
        }
    }
    mavenCentral()
}

dependencies {
    implementation enforcedPlatform("${quarkusPlatformGroupId}:${quarkusPlatformArtifactId}:${quarkusPlatformVersion}")
    implementation "io.quarkus:quarkus-arc"
    testImplementation "io.quarkus:quarkus-junit5"
}

sourceSets {
    // Functional Tests
    funcTest {
        java.srcDir 'src/funcTest/java'
        if (file('src/funcTest/resources').exists()) {
            resources.srcDir 'src/funcTest/resources'
        }

        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}

compileJava {
    options.compilerArgs << '-parameters'
}

/*
 * Functional tests for this module.
 */
task functionalTest(type: Test, description: 'Functional tests', dependsOn: [processResources, classes]) {
    description = 'Run the functional tests.'
    group = 'verification'

    useJUnitPlatform()
    testClassesDirs = sourceSets.funcTest.output.classesDirs
    classpath = sourceSets.funcTest.runtimeClasspath

    // Show standard streams on the console.
    testLogging {
        showStandardStreams = true
    }
    systemProperty 'quarkus.http.host', 'localhost'
    systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
}

test {
    systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
}
