plugins {
    id 'com.gradle.plugin-publish' version '0.11.0'
    id 'java-gradle-plugin'
}
if (JavaVersion.current().isJava9Compatible()) {
    compileJava.options.compilerArgs.addAll(['--release', '8'])
}
compileJava {
    options.encoding = 'UTF-8'
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    api gradleApi()
    implementation "io.quarkus:quarkus-bootstrap-core:${version}"
    implementation "io.quarkus:quarkus-devtools-common:${version}"
    implementation "io.quarkus:quarkus-platform-descriptor-json:${version}"
    implementation "io.quarkus:quarkus-platform-descriptor-resolver-json:${version}"
    implementation "io.quarkus:quarkus-core-deployment:${version}"

    testImplementation 'org.mockito:mockito-core:3.3.3'
    testImplementation 'org.assertj:assertj-core:3.16.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.6.2'
    testImplementation 'org.awaitility:awaitility:4.0.2'
    testImplementation 'org.jprocesses:jProcesses:1.6.5'
    testImplementation "io.quarkus:quarkus-devmode-test-utils:${version}"
    testImplementation gradleTestKit()
}

test {
    testLogging {
        events "passed", "skipped", "failed"
    }
    useJUnitPlatform()
}

javadoc {
    options.addStringOption('encoding', 'UTF-8')
}

pluginBundle {
    website = 'https://quarkus.io/' 
    vcsUrl = 'https://github.com/quarkusio/quarkus' 
    tags = ['quarkus', 'quarkusio', 'graalvm'] 
}

gradlePlugin {
    plugins {
        quarkusPlugin {
            id = 'io.quarkus'
            implementationClass = 'io.quarkus.gradle.QuarkusPlugin'
            displayName = 'Quarkus Plugin'
            description = 'Builds a Quarkus application, and provides helpers to launch dev-mode, the Quarkus CLI, building of native images'
        }
    }
}

// Add a source set for the functional test suite
sourceSets {
    functionalTest {
    }
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)
configurations.functionalTestImplementation.extendsFrom(configurations.testImplementation)
configurations.functionalTestRuntime.extendsFrom(configurations.testRuntimeOnly)

// Add a task to run the functional tests
task functionalTest(type: Test) {
    description = "Runs functional tests"
    group = "verification"
    useJUnitPlatform()
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath

    // Kotlin compiler does not support Java 14
    if(JavaVersion.current() == JavaVersion.VERSION_14) {
        exclude 'io/quarkus/gradle/devmode/MultiModuleKotlinProjectDevModeTest.class'
    }
}

check {
    // Run the functional tests as part of `check`
    dependsOn(tasks.functionalTest)
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}
